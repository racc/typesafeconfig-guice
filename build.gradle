apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
	mavenCentral()
}

group = 'com.github.racc'
archivesBaseName = "typesafeconfig-guice"
version = '0.0.3'

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}
 
task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
	archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

def uzer = hasProperty('ossrhUsername') ? ossrhUsername : System.getenv('ossrhUsername')
def password = hasProperty('ossrhPassword') ? ossrhPassword : System.getenv('ossrhPassword')

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
 
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
              	authentication(userName: uzer, password: password)
            }
            
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        		authentication(userName: uzer, password: password)
      		}
 
            pom.project {
               name 'TypesafeConfigGuice'
               packaging 'jar'
               description 'TypesafeConfigGuice allows TypesafeConfig values to be injected with Guice'
               url 'https://github.com/racc/typesafeconfig-guice'

               scm {
                   url 'scm:git@github.com:racc/typesafeconfig-guice.git'
                   connection 'scm:git@github.com:racc/typesafeconfig-guice.git'
                   developerConnection 'scm:git@github.com:racc/typesafeconfig-guice.git'
               }
 
               licenses {
                   license {
                       name 'The Apache Software License, Version 2.0'
                       url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                       distribution 'repo'
                   }
               }
 
               developers {
                   developer {
                       id 'racc'
                       name 'Jason Then'
                   }
               }
           }
        }
    }
}

dependencies {
	compile 'com.typesafe:config:1.3.0'
	compile 'org.reflections:reflections:0.9.9'
	compile 'com.google.inject:guice:4.0:no_aop'
	
	testCompile 'junit:junit:4.12'
}
